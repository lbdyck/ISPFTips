 /*----------------------------------------------------------------rexx
   RXISPTBL table subroutine demo
   Call self for primary- and line commands
   Accept all built-in line commands.
   Table variable list: zsel data
 --------------------------------------------------------------------*/

 /* initiaization */
 parse source . . $me .
 parse value 'PNWJTBL4 PNWJTBD4 TBWJTBD0' with lpnlname dpnlname tblname .
 pid   = right(left(space(translate(time('l'),' ','.:'),0),10),7)
 Address ispexec
 "control errors return"

 /* determine if called as command handler */
 arg p1,pid,pcmd,ptable,ptext
 if p1 pcmd='RXISPTBL CMD' then Exit PCmdX()
 if p1 pcmd='RXISPTBL SEL' then Exit LCmdX()

 /* call driver, list panel only */
 Call Prolog
 cc=RXISPTBL('table('tblname') panel('lpnlname') width(80)',
           'proc('$me') id('pid') sel(s e v) hdr(Table driver demo 2)',
           'sort(=) icmd(sort) isel(all) dpanel('dpnlname')')

 /* cleanup and terminate */
 Call Epilog

Ispmsg:
 parse arg zedlmsg
 address ispexec"setmsg msg(isrz000)"
 return 0

 /*- Line command handlers -*/
LCmdX:
 "tbget" ptable
 err=''
 dsn=strip(data)
 Select
   when zsel='E' then "Edit dataset('"dsn"')"
   when zsel='V' then "View dataset('"dsn"')"
   when zsel='S' then do
     cc=listdsi("'"dsn"'")
     call ispmsg dsn sysvolume sysdsorg syslrecl sysblksize
   end
   otherwise err='Y'
 End
 if err='Y' then zsel='?'zsel
 else zsel=''
 "tbput" ptable
 exit 0

 /*- Primary command handlers -*/
PCmdX:
 call ispmsg 'Pcmd' ptext 'not implemeted yet'
 exit 0

 /* init, terminate */
Prolog:
 "tbend" tblname
 "tbcreate" tblname "names(zsel data) nowrite share"
 if rc<>0 then exit xmsg('table create rc' rc,8)

 /* populate table */
 zz=outtrap('lst.')
 Address TSO "listalc"
 zz=outtrap('off')
 zsel=''
 t.=''                       /* drop non-unique */
 do n=1 to lst.0
   data=strip(lst.n)
   if t.data<>'',
   |  data='NULLFILE' | data='TERMFILE',
   |  right(data,1)='?' then iterate
   "tbadd" tblname
   if rc>0 then call xreturn 'tbadd failed' rc zerrlm
   t.data='x'
 end
 drop t.
 Return 0

Epilog:
 address ispexec "tbend" tblname
 if arg(1)<>'' then say arg(1)
 exit 0
