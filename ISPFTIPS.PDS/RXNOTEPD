  /* ------------------------- REXX ------------------------------- *
  | Name: RXNOTEPD  (REXX NotePad)                                 |
  |                                                                |
  | Provide quick access to a z/OS PDS to track notes and anything |
  | else of interest.                                              |
  |                                                                |
  | Create an ISPF Command Table entry similar to this:            |
  |                                                                |
  | Verb:   NOTEPAD                                                |
  | Trunc:  4                                                      |
  | Description: ISPF Notepad                                      |
  | Action: SELECT CMD(%RXNOTEPD &ZPARM)                           |
  |                                                                |
  | Find *CUSTOM* below to update site customizations.             |
  |                                                                |
  | Then from any ISPF panel enter NOTE or NOTE member-name        |
  |                                                                |
  | Uses userid.NOTEPAD.PDSE or user-prefix.NOTEPAD.PDSE           |
  | and will allocate it if it doesn't exist                       |
  * -------------------------------------------------------------- *
  | ISPF - Developers - Tips and Tricks                            |
  * -------------------------------------------------------------- */
  arg note

  /* -------*CUSTOM*--------------- *
  | Set the desired editor to use: |
  | 1 - PDSEGEN                    |
  | 2 - ISPF Edit                  |
  | 3 - ISPF Edit Member List      |
  * ------------------------------ */
  Editor = 3   /* change per above */

  /* ---------- *CUSTOM* -------------- *
  | Construct the notepad dataset name |
  * ---------------------------------- */
  if sysvar('syspref') /= ''
  then notesdsn = sysvar('syspref')'.NOTEPAD.PDSE'
  else notesdsn = sysvar('sysuid')'.NOTEPAD.PDSE'

  /* ------------------------------------------------------- *
  | If the notepad dataset does not exist then allocate it. |
  | If using PDSEGEN, or some other editor that supports    |
  | member generations then change the MAXGEN from 0 to     |
  | something more useful.                                  |
  * ------------------------------------------------------- */
  if sysdsn("'"notesdsn"'") /= 'OK' then do
    "Alloc ds('"notesdsn"') new spa(30,30) tr recfm(f b) lrecl(80)" ,
      'blksize(32720) dir(1) dsntype(library,2) maxgen(0)'
    "Free ds('"notesdsn"')"
  end

  Select
    When editor = 1 then do
      /* ------------------------------------------- *
      | Use PDSEGEN to read/update/create the notes |
      * ------------------------------------------- */
      "%pg '"notesdsn"'" note
    end

    When editor = 2 then do
      /* --------------------------------------------- *
      | Use ISPF Edit to read/update/create the notes |
      * --------------------------------------------- */
      if note /= ''
      then notesdsn = notesdsn'('note')'
      Address ISPExec ,
        "Edit dataset('"notesdsn"')"
    end

    When editor = 3 then do
      /* --------------------------------------------- *
      | Use ISPF Edit to read/update/create the notes |
      * --------------------------------------------- */
      Address ISPExec
      "LMInit Dataid(notes) Dataset('"notesdsn"')"
      "Memlist Dataid("notes") Default(E) Member("note")"
      "LMfree Dataid("notes")"
    end
    Otherwise nop
  end

  Exit
